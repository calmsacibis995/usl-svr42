#ident	"@(#)oamintf:common/cmd/oamintf/usermgmt/Form.addgrp	1.2.10.3"
#ident  "$Header: Form.addgrp 2.0 91/07/12 $"

################################################################################
#	Module Name:  Form.addgrp
# 	Primary Command:
#		 groupadd [-g gid [-o] group
#
################################################################################
form="Add a Group"

`/usr/bin/rm -f /tmp/ln`

framemsg=`readfile $INTFBASE/form.msg`

help=OPEN TEXT $INTFBASE/Text.itemhelp $LININFO

close=`/usr/bin/rm -f /tmp/gadderr`

done=`message -w "Adding group $F1 to the system.";

	/usr/bin/rm -f /tmp/ln;

	/usr/sadm/sysadm/bin/addgrp $F1 $F2 $F3 "$F4";

	if [ $RET -eq 0 ];
	then
      		set -l CMD="OPEN TEXT $OBJ_DIR/Text.agrpok $F1";
	elif [ $RET -eq 2 ];
	then
      		set -l CMD="OPEN TEXT $OBJ_DIR/Text.badprim $F1";
	elif [ $RET -eq 3 ];
	then
      		set -l CMD="OPEN TEXT $OBJ_DIR/Text.badsup $F1";
	else
     		set -l CMD="OPEN TEXT $OBJ_DIR/Text.agrpnok $F1";
	fi`$CMD

#  $F1
name=Group name:
lininfo=Form.addgrp:grpname
nrow=1
ncol=3
frow=1
fcol=15
rows=1
columns=8

valid=`/usr/sadm/bin/valstr -r '^[ -9;-z]\{0,8\}$' $F1 && /usr/sadm/sysadm/bin/uniq_gnam $F1`
invalidmsg=`if [ $RET -eq 2 ];
	    then
		echo "ERROR: Group name must be unique.";
	    else
		echo "ERROR: Please enter a character string 1-8 characters, cannot include a (:).";
	    fi`

# $F2
name=Group ID:
lininfo=Form.addgrp:grpid
nrow=2
ncol=3
frow=2
fcol=13
rows=1
columns=5

value=const `/usr/sadm/sysadm/bin/findgid`
`/usr/sadm/sysadm/bin/getmaxuid | set -l MAXUID`
valid=`/usr/sadm/bin/valrange -l100 -u$MAXUID $F2 && /usr/sadm/sysadm/bin/uniq_gid $F2`
invalidmsg=`if [ $RET -eq 2 ];
	    then
		echo "ERROR: Group ID must be unique.";
	    else
		/usr/sadm/bin/errange -l100 -u$MAXUID;
	    fi`

# $F3
name=Primary member(s):
lininfo=Form.addgrp:primem
nrow=3
ncol=3
frow=3
fcol=22
rows=1
columns=55
scroll=true

rmenu=OPEN MENU $OBJ_DIR/Menu.mlogins
valid=`/usr/sadm/sysadm/bin/chk_logins "$F3"`
invalidmsg=`if [ $RET -eq 1 ];
	    then
		if [ -f /tmp/ln ];
	    	then
			readfile /tmp/ln | set -l BSG;
			echo "ERROR: Login '$BSG' does not exist.";
			unset -l BSG;
	    	else
			echo "ERROR: Login names must be comma separated";
	    	fi;
	    	/usr/bin/rm -f /tmp/ln;
	    fi`

# $F4
name=Supplementary member(s):
lininfo=Form.addgrp:supmem
nrow=4
ncol=3
frow=4
fcol=28
rows=1
columns=49
scroll=true

rmenu=OPEN MENU $OBJ_DIR/Menu.mlogins
valid=`/usr/sadm/sysadm/bin/chk_logins $F4`
invalidmsg=`if [ $RET -eq 1 ];
	    then
		if [ -f /tmp/ln ];
	    	then
			readfile /tmp/ln | set -l BSG;
			echo "ERROR: Login '$BSG' does not exist.";
			unset -l BSG;
	    	else
			echo "ERROR: Login names must be comma separated";
	    	fi;
	    	/usr/bin/rm -f /tmp/ln;
	    fi`

name=RESET
button=8
action=RESET
