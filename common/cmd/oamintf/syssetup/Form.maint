#ident	"@(#)oamintf:common/cmd/oamintf/syssetup/Form.maint	1.2.3.4"
#ident  "$Header: Form.maint 2.1 91/09/18 $"
Form="Maintain files in /etc/default"

help=OPEN TEXT $INTFBASE/Text.itemhelp $LININFO

framemsg=`readfile $INTFBASE/form.msg`

close=`rm -f $MAINTERR`

done=`set -l MAINTERR=/tmp/$VPID.mainterr;
	unset -l MAINTARGS;
	getfrm |set -l FRAMEID;
	if [ "$F2" = "remove" ];
	then
		set -l MAINTARGS="-d $BASEFN $F3";
	else
		set -l MAINTARGS="$BASEFN $F3=$F4";
	fi;
	shell "$TFADMIN /usr/bin/defadm $MAINTARGS" >/dev/null 2>$MAINTERR;
	if [ $RET -eq 0 ];
	then
		set -l CMD="OPEN TEXT $OBJ_DIR/Text.mok $BASEFN";
	else
		set -l CMD="OPEN TEXT $OBJ_DIR/Text.mnok $BASEFN";
	fi`$CMD

#F1
name="Filename:"
lininfo=Form.maint:filename
nrow=1
ncol=3
frow=1
fcol=13
rows=1
columns=20
scroll=TRUE
rmenu={ `defadm` }
valid=vary `
echo "F1=$F1" >/tmp/trace;
	if [ -n "$F1" -a -f /etc/default/$F1 ];
	then
		message -t "Please wait.";
		/usr/bin/basename "$F1" |set -l BASEFN;
		$TFADMIN defadm $BASEFN | fmlcut -d= -f1 >/tmp/$VPID.names;
	else 
		echo FALSE;
	fi`
invalidmsg= const "Error = Must enter an existing file in /etc/default."
fieldmsg= const "Press the CHOICES function key. Press SAVE when you complete the form."

#F2
name="Do you want to add, change, or remove names?"
lininfo=Form.maint:action
nrow=2
ncol=3
frow=2
fcol=48
rows=1
columns=7
value=add
rmenu={ add change remove }
menuonly=TRUE
invalidmsg= const "Error = Must enter add, change, or remove."
fieldmsg= const "Enter add, change, or remove."

#F3
name="Name:"
lininfo=Form.maint:name
nrow=3
ncol=3
frow=3
fcol=9
rows=1
columns=20
scroll=TRUE
rmenu=vary { `
	if [ "$F2" != "add" ];
	then
		/usr/bin/cat /tmp/$VPID.names; 
	fi` }
valid=vary `
	if [ -n "$F3" ];
	then
		if [ "$F2" = "add" ];
		then
			/usr/bin/grep "^$F3\$" /tmp/$VPID.names >/dev/null 2>/$MAINTERR;
			if [ $RET -eq 0 ];
			then
				echo FALSE;
			else
				echo TRUE;
			fi;
		else
			/usr/bin/grep "^$F3\$" /tmp/$VPID.names >/dev/null 2>$MAINTERR;
			if [ $RET -eq 0 ];
			then
				echo TRUE;
			else
				echo FALSE;
			fi;
			
		fi;
	else 
		echo FALSE;
	fi`
invalidmsg=`regex -e -v "$F2" 'add' 'Error - Enter a unique name.'
			'change' 'Error - Enter an existing name.'
			'remove' 'Error - Enter an existing name.'`
fieldmsg=`regex -e -v "$F2" 'add' 'Enter a unique name.'
			'change' 'Press the CHOICES function key. Press SAVE when you complete the form.'
			'remove' 'Press the CHOICES function key. Press SAVE when you complete the form.'`

#F4
name="Value:"
lininfo=Form.maint:value
nrow=4
ncol=3
frow=4
fcol=10
rows=1
columns=20
value=vary `
	if [ "$F2" != "add" -a -n "$F3" ];
	then
		$TFADMIN /usr/bin/defadm $BASEFN $F3 | fmlcut -d= -f2 | set -l VAL;
		if [ -n $VAL ];
		then
			echo $VAL;
		else
			echo "";
		fi;
		unset -l VAL;
	fi;`
scroll=TRUE
inactive=`[ "$F2" = "remove" ]`
#valid=`test -n "$F4"`
#invalidmsg= const "Error = Must enter a value for name."
fieldmsg= const "Enter a value for name."
